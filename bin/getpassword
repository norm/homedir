#!/ports/bin/perl
# get a password stored in the OS X Keychain

use Modern::Perl;

my $label = shift
            or die "Usage: getpassword <keychain label>";

open
    my $pipe,
    qq(security find-generic-password -gl "${label}" 2>&1 >/dev/null |)
        or die "Cannot run security: $!";
my $content = <$pipe>;

$content =~ m{
        ^
            password: \s*
            (?:                             # optional...
                0x
                (?<hex> 
                    [0-9A-F]+               # ...hex encoded version
                )
            )?
            \s+
            "(?<str> 
                .*                          # string version
            )"
        $
    }x;

# use the hex in preference to the string version
if ( defined $+{'hex'} ) {
    my $hex = $+{'hex'};
    
    $hex =~ s{(..)}{ chr( hex($1) ) }eg;
    say $hex;
}
else {
    say $+{'str'};
}
