#!/usr/bin/env perl
# github:norm:homedir:bin/getpassword
# -*- Mode: Perl; tab-width: 4; indent-tabs-mode: nil; -*-
#
#       Look up a password stored within any accessible OS X Keychain.
#
#       OS X will prompt if the keychain file is currently locked; the script 
#       will wait until it is unlocked, or the password entry is cancelled.

use 5.10.0;

my $label = shift
    or die "Usage: getpassword <entry label>";

open
    my $pipe,
    qq(security find-generic-password -gl "${label}" 2>&1 >/dev/null |)
        or die "Cannot run security: $!";

my $content = <$pipe>;

$content =~ m{
        ^
            password: \s*
            (?:
                0x
                (?<hex>                     # $+{'hex'}:
                    [0-9A-F]+               # optional hex encoded version
                )
            )?
            \s+
            "(?<str>                        # $+{'string'}:
                .*                          # string version
            )"
        $
    }x;

# use the hex in preference to the string version
if ( defined $+{'hex'} ) {
    my $hex = $+{'hex'};
    
    $hex =~ s{(..)}{ chr( hex($1) ) }eg;
    say $hex;
}
else {
    say $+{'str'};
}
